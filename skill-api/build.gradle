buildscript {
    repositories {
        mavenLocal()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        mavenCentral()
        maven {
            name = "mojang"
            url = "https://libraries.minecraft.net/"
        }
    }
    dependencies {
        classpath group: 'com.anatawa12.forge', name: 'ForgeGradle', version: '1.2-1.0.+', changing: true
    }
}

plugins {
    id 'java-library'
}

apply plugin: 'forge'

version = "0.1"
archivesBaseName = "SkillAPI GenericPack"
group = "com.github.junzzzz"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.7.10-10.13.4.1558-1.7.10"
    runDir = "minecraft"
}

ext {
    jacksonVersion = '2.6.7'
    lombokVersion = '1.18.22'
    cglibVersion = '3.2.1'
}

dependencies {
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
//    annotationProcessor project(':skill-processor')

    implementation group: 'cglib', name: 'cglib', version: cglibVersion

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
}

runClient {
    args '--username', 'Junzzzz'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    // copy everything else, thats not mcmod.info
    from(project.sourceSets.main.resources.srcDirs) {
        include("*.png", "*.lang")
    }
}

jar {
    // Keep the jar as clean as possible
    includeEmptyDirs = false
}

task unJar(type: Copy, dependsOn: 'build') {
    from zipTree(jar.archiveFile)
    into 'build/unjar'
}

task buildAPI(type: Jar, dependsOn: 'unJar') {
    archiveFileName = "[" + minecraft.version + "] SkillAPI-" + version + "." + extension
    from 'build/unjar'
    include('assets/skillapi/**', 'skillapi/**')
    // Mod info
    from('api-info') {
        include('mcmod-api.info', 'pack.mcmeta')
        expand([version: archiveVersion, mcversion: minecraft.version])
    }
}

task buildPack(type: Jar, dependsOn: 'unJar') {
    archiveFileName = "[" + minecraft.version + "] GenericSkill Pack-" + version + "." + extension
    from 'build/unjar'
    include('assets/genericskills/**', 'genericskills/**')
    // Mod info
    from('pack-info') {
        include('mcmod-pack.info')
        expand([version: archiveVersion, mcversion: minecraft.version])
    }
}

task buildBoth(dependsOn: ['buildAPI', 'buildPack'])